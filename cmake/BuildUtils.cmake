function(cepgen_build mod_name)
    set(options INSTALL_COMPONENT)
    set(one_val_args "")
    set(multi_val_args SOURCES EXT_LIBS EXT_HEADERS DEFINITIONS PROPERTY)
    cmake_parse_arguments(ARG "${options}" "${one_val_args}" "${multi_val_args}" ${ARGN})
    message(STATUS "Will build ${mod_name} add-on")
    if(ARG_SOURCES)
        set(sources)
        foreach(_s ${ARG_SOURCES})
            file(GLOB s "${_s}")
            list(APPEND sources ${s})
        endforeach()
        add_library(${mod_name} SHARED ${sources})
        if(ARG_EXT_LIBS)
            message(STATUS "... external libraries: ${ARG_EXT_LIBS}")
            target_link_libraries(${mod_name} ${ARG_EXT_LIBS})
        endif()
        target_include_directories(${mod_name} INTERFACE ${PROJECT_SOURCE_DIR})
        if(ARG_EXT_HEADERS)
            message(STATUS "... external headers: ${ARG_EXT_HEADERS}")
            target_include_directories(${mod_name} PRIVATE ${ARG_EXT_HEADERS})
        endif()
        if(ARG_PROPERTY)
            set_property(TARGET ${mod_name} PROPERTY ${ARG_PROPERTY})
        endif()
        if(ARG_DEFINITIONS)
            target_compile_definitions(${mod_name} PRIVATE ${ARG_DEFINITIONS})
        endif()
        list(APPEND CEPGEN_ADDONS_EXT ${mod_name})
    else() # interfacing library (only includes)
        add_library(${mod_name} INTERFACE)
        target_include_directories(${mod_name} INTERFACE ${PROJECT_SOURCE_DIR})
    endif()
    if(ARG_INSTALL_COMPONENT)
        #----- installation rules
        install(TARGETS ${mod_name} DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT ${ARG_INSTALL_COMPONENT})
        install(DIRECTORY DESTINATION ${CMAKE_INSTALL_INCLUDEDIR} COMPONENT ${ARG_INSTALL_COMPONENTS} FILES_MATCHING PATTERN "*.h")
    endif()
    set(CEPGEN_ADDONS_EXT ${CEPGEN_ADDONS_EXT} PARENT_SCOPE)
endfunction()

